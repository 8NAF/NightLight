def taskRequests = getGradle().getStartParameter().getTaskRequests().toString()
def isPlayStore = taskRequests.contains("generic") || taskRequests.contains("Generic") || taskRequests.contains("donate") || taskRequests.contains("Donate")

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

if (isPlayStore) {
    apply plugin: 'io.fabric'
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    defaultConfig {
        applicationId "com.corphish.nightlight"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 53
        versionName "2.6.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions "buildVariant"

    productFlavors {
        generic {
            applicationId "com.corphish.nightlight.generic"
            dimension "buildVariant"
        }
        donate {
            applicationId "com.corphish.nightlight.donate"
            dimension "buildVariant"
        }
        foss {
            applicationId "com.corphish.nightlight.generic"
            dimension "buildVariant"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.2.0-alpha06'

    implementation 'eu.chainfire:libsuperuser:1.0.0.201704021214'

    implementation 'com.luckycatlabs:SunriseSunsetCalculator:1.2'
    implementation 'com.github.mc0239:EditableSeekBar:v1.3.0'

    implementation 'com.github.corphish:widgets-ktx:0.3.2'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.2.2'
    implementation 'androidx.navigation:navigation-ui:2.2.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'
    genericImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    donateImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation "androidx.preference:preference-ktx:1.1.1"
}
